#Options
GenerateScanner = true
GenerateParser = true
Language = Java
ScannerName = Lexico
ParserName = Sintatico
SemanticName = Semantico
ScannerCaseSensitive = true
ScannerTable = Compact
Input = Stream
Parser = LL
#RegularDefinitions
//Definições Regulares:

mai:[A-Z]
min:[a-z]
dig:[0-9]
posit:[1-9]

letra: {mai} | {min}


#Tokens
//Tokens:

identificador:({min} | ({mai} {min}+))+ (_{dig}+)?

cint: ({posit} {dig}? {dig}? {dig}? {dig}?) | 0

cfloat: (({posit} {dig}? {dig}? {dig}? {dig}?) | 0)"."({dig}? {dig}? {dig}? {dig}? {posit}) | 0"."0 | ({posit} {dig}? {dig}? {dig}? {dig}?)"."0 


cstring: \"[^\n\"\\]*\"


// reservadas
pr_add         = identificador: "add"
pr_and         = identificador: "and"
pr_begin       = identificador: "begin"
pr_bool        = identificador: "bool"	
pr_count       = identificador: "count"
pr_delete      = identificador: "delete"
pr_do          = identificador: "do"
pr_elementOf   = identificador: "elementOf"
pr_else        = identificador: "else"
pr_end         = identificador: "end"
pr_false       = identificador: "false"
pr_float       = identificador: "float"
pr_if          = identificador: "if"
pr_int         = identificador: "int"
pr_not         = identificador: "not"
pr_or          = identificador: "or"
pr_print       = identificador: "print"
pr_list        = identificador: "list"
pr_read        = identificador: "read"
pr_size        = identificador: "size"
pr_string      = identificador: "string"
pr_true        = identificador: "true"
pr_until       = identificador: "until"

// Símbolos especiais
"+"
"-"
"*"
"/"
"=="
"~="
"<"
">"
"="
"<-"
"("
")"
";"
","


// Reconhecer e ignorar caracteres de formatação
:[\s\t\n]

// Reconhecer e ignorar comentários
:!"{" [^\{\}]* "}"
:!"--"[^\n]*

#NonTerminals
<programa>
<lista_instruções>
<instrução>
<dec_variável>
<comando>
<tipo>
<lista>
<lista_id>
<elemento>
<posição>
<ent_dados>
<lista_expressões>
<id_com_str>
<string_virg>
<lista_entrada>
<com_saida>
<com_sel>
<lista_comandos>
<com_else>
<com_rep>
<simples>
<lista_instruções1>
<comando1>
<lista_id1>
<lista_entrada1>
<lista_comandos1>
<expressao>
<valor>
<expressao_>
<relacional>
<relacional_>
<aritmetica>
<operador_relacional>
<aritmetica_>
<termo>
<termo_>
<fator>
<fator_>
<lista_expressões1>
#Grammar
<programa> ::= #100 pr_begin <lista_instruções> pr_end #101 ;

<lista_instruções> ::= <instrução> ";" <lista_instruções1> ;
<lista_instruções1> ::= î | <lista_instruções> ;

<instrução> ::= <dec_variável> | <comando> ;

<dec_variável> ::= <tipo> <lista_id> #119 ;

<comando> ::= identificador <comando1> | <ent_dados> | <com_saida> | <com_sel> | <com_rep> ;
<comando1> ::= "=" <expressao> | "<-" <expressao> | pr_add "(" <elemento> "," <posição> ")" | pr_delete "(" <posição> ")";

<tipo> ::= pr_bool #120 | pr_int #120 | pr_float #120 | pr_string #120 | <lista> ;
<lista> ::= pr_list "(" <simples> "," cint ")" ;

<lista_id> ::= identificador #121 <lista_id1> ;
<lista_id1> ::= î | "," <lista_id> ;

<elemento> ::= <expressao> ;
<posição> ::= <expressao> ;

<ent_dados> ::= pr_read "(" <lista_entrada> ")" ;

<lista_expressões> ::= <expressao> #102 <lista_expressões1>;
<lista_expressões1> ::= î | "," <lista_expressões>;
<id_com_str> ::= <string_virg> identificador ;
<string_virg> ::= cstring "," | î ;

<lista_entrada> ::= <id_com_str> <lista_entrada1> ;
<lista_entrada1> ::= î | "," <lista_entrada> ;

<com_saida> ::= pr_print "(" <lista_expressões> ")" ;
<com_sel> ::= pr_if <expressao> <lista_comandos> <com_else> pr_end ;

<lista_comandos> ::= <comando>  ";" <lista_comandos1> ;
<lista_comandos1> ::= î | <lista_comandos> ;


<com_else> ::= pr_else <lista_comandos> | î ;
<com_rep> ::= pr_do <lista_comandos> pr_until <expressao> ;

<simples> ::= pr_bool | pr_int | pr_float | pr_string ;


<expressao> ::= <valor> <expressao_> ;
<expressao_> ::= î | pr_and <valor> <expressao_> | pr_or <valor> <expressao_> ;
<valor> ::= <relacional> | pr_true | pr_false | pr_not <valor> ;
<relacional> ::= <aritmetica> <relacional_> ;
<relacional_> ::= î | <operador_relacional> <aritmetica> ;
<operador_relacional>::= "==" | "~=" | "<" | ">" ;
<aritmetica> ::= <termo> <aritmetica_> ;
<aritmetica_> ::= î | "+" <termo> <aritmetica_> | "-" <termo> <aritmetica_> ;
<termo> ::= <fator> <termo_> ;
<termo_> ::= î | "*" <fator> <termo_> | "/" <fator> <termo_> ;
<fator> ::= identificador #130 <fator_> |
 cint #103 |
 cfloat #104 |
 cstring #105 |
 "(" <expressao> ")" |
 "+" <fator> |
 "-" <fator> ;
<fator_> ::= î | pr_count | pr_size | pr_elementOf "(" <expressao> ")" ; 

